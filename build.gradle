group 'Proto'
version '1.0'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/source/proto/main/javalite"
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite { artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0' }
        ruby { path = 'grpc_tools_ruby_protoc' } //gem install grpc-tools
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins { remove java } //Don't generate default java, we are using javalite
            task.plugins {
                //All supported languages
                javalite {}
                ruby {}
            }
        }
    }

}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

afterEvaluate {
    compileKotlin.dependsOn generateProto
}
